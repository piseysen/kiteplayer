apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.google.gms.google-services'

android {
    signingConfigs {
        if (project.hasProperty('storeFile')) {
            release {
                storeFile file(project.storeFile)
                storePassword project.storePassword
                keyAlias project.keyAlias
                keyPassword project.keyPassword
            }
        } else {
            release.initWith(debug)
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    defaultConfig {
        applicationId "com.misterpereira.android.kiteplayer"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 2
        versionName '1.0.1'

        buildConfigField 'String', 'dbApiKey', '"a7hfbbfwl9n54k7"'
        buildConfigField 'String', 'dbApiSecret',
                project.hasProperty('dbApiSecret')?"\"${project.dbApiSecret}\"":'""'
    }
    buildTypes {
        release {
            minifyEnabled false // TODO Fix proguard
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-retrolambda.pro', 'proguard-rxjava.pro', 'proguard-glide.pro'
            signingConfig signingConfigs.release
            zipAlignEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
    productFlavors {
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.google.android.gms:play-services-cast:8.3.0'
    compile 'com.google.android.support:wearable:+'
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:mediarouter-v7:23.1.0'
    compile 'com.android.support:leanback-v17:23.1.0'
    compile(name: 'CastCompanionLibrary-debug', ext: 'aar')
    compile 'com.github.amlcurran.showcaseview:library:5.0.0'
    // Google Analytics
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    // NanoHTTPD
    compile 'com.nanohttpd:nanohttpd:2.1.1'
    // Glide
    compile 'com.github.bumptech.glide:glide:3.6.+'
    compile 'com.github.bumptech.glide:okhttp-integration:1.3.+'
    // RxJava / RxAndroid
    compile 'io.reactivex:rxjava:1.0.+'
    compile 'io.reactivex:rxjava-async-util:0.21.+'
    compile 'io.reactivex:rxandroid:0.25.+'
    // Dagger 2
    compile 'com.google.dagger:dagger:2.+'
    apt 'com.google.dagger:dagger-compiler:2.+'
    // Java APIs
    compile 'javax.annotation:javax.annotation-api:1.2'
    // Stetho
    compile 'com.facebook.stetho:stetho:1.2.0'
    compile 'com.android.support:support-annotations:23.1.0'
}
